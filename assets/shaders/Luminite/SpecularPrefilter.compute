#version 430

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(binding = 0) uniform samplerCube u_EnvironmentMap;
layout(binding = 0, rgba16f) uniform writeonly image2D u_PrefilterMap;

uniform int u_Face;  // Which cubemap face to process (0-5)

uniform float u_Resolution;
uniform float u_Roughness;

const float PI = 3.14159265359;

// Importance sampling for GGX
vec3 ImportanceSampleGGX(vec2 Xi, vec3 N, float roughness) {
    float a = roughness * roughness;
    
    float phi = 2.0 * PI * Xi.x;
    float cosTheta = sqrt((1.0 - Xi.y) / (1.0 + (a * a - 1.0) * Xi.y));
    float sinTheta = sqrt(1.0 - cosTheta * cosTheta);
    
    // Spherical to cartesian
    vec3 H;
    H.x = cos(phi) * sinTheta;
    H.y = sin(phi) * sinTheta;
    H.z = cosTheta;
    
    // Tangent space to world space
    vec3 up = abs(N.z) < 0.999 ? vec3(0.0, 0.0, 1.0) : vec3(1.0, 0.0, 0.0);
    vec3 tangent = normalize(cross(up, N));
    vec3 bitangent = cross(N, tangent);
    
    vec3 sampleVec = tangent * H.x + bitangent * H.y + N * H.z;
    return normalize(sampleVec);
}

// Hammersley sequence for low-discrepancy sampling
vec2 Hammersley(uint i, uint N) {
    return vec2(float(i) / float(N), float(bitfieldReverse(i)) * 2.3283064365386963e-10);
}

void main() {
    ivec2 globalID = ivec2(gl_GlobalInvocationID.xy);
    if (globalID.x >= int(u_Resolution) || globalID.y >= int(u_Resolution)) {
        return;
    }
    
    // Convert face index and local coordinates to direction
    vec2 localUV = (vec2(globalID) + 0.5) / u_Resolution;
    localUV = localUV * 2.0 - 1.0;  // -1 to 1
    
    vec3 N = vec3(0.0);
    
    // Generate normal direction based on cubemap face
    if (u_Face == 0) {      // +X
        N = normalize(vec3(1.0, -localUV.y, -localUV.x));
    } else if (u_Face == 1) { // -X
        N = normalize(vec3(-1.0, -localUV.y, localUV.x));
    } else if (u_Face == 2) { // +Y
        N = normalize(vec3(localUV.x, 1.0, localUV.y));
    } else if (u_Face == 3) { // -Y
        N = normalize(vec3(localUV.x, -1.0, -localUV.y));
    } else if (u_Face == 4) { // +Z
        N = normalize(vec3(localUV.x, -localUV.y, 1.0));
    } else if (u_Face == 5) { // -Z
        N = normalize(vec3(-localUV.x, -localUV.y, -1.0));
    }
    
    vec3 R = N;
    vec3 V = R;
    
    const uint SAMPLE_COUNT = 1024u;
    vec3 prefilteredColor = vec3(0.0);
    float totalWeight = 0.0;
    
    for (uint i = 0u; i < SAMPLE_COUNT; ++i) {
        vec2 Xi = Hammersley(i, SAMPLE_COUNT);
        vec3 H = ImportanceSampleGGX(Xi, N, u_Roughness);
        vec3 L = normalize(2.0 * dot(V, H) * H - V);
        
        float NdotL = max(dot(N, L), 0.0);
        if (NdotL > 0.0) {
            prefilteredColor += texture(u_EnvironmentMap, L).rgb * NdotL;
            totalWeight += NdotL;
        }
    }
    
    prefilteredColor = prefilteredColor / totalWeight;
    
    imageStore(u_PrefilterMap, globalID, vec4(prefilteredColor, 1.0));
}

